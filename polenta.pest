integer    = @{ ASCII_DIGIT+ }
identifier = @{ !"let" ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC)* }

atom    = _{ unary_op? ~ primary }
primary = _{ identifier ~ "{" ~ expr ~ "}" | "(" ~ expr ~ ")" | integer | identifier }

/// Unary operations
unary_op = _{ minus }
minus    =  { "-" }

/// Binary operations
bin_op   = _{ add | subtract | multiply | divide | modulo | power }
add      =  { "+" }
subtract =  { "-" }
multiply =  { "*" }
divide   =  { "/" }
modulo   =  { "%" }
power    =  { "^" }

// TODO: these should be statements

expr          = { atom ~ (bin_op ~ atom)* }
let_expr      = { "let" ~ identifier ~ "=" ~ expr ~ ";" }
let_poly_expr = { "let" ~ identifier ~ "(" ~ identifier ~ ")" ~ "=" ~ expr ~ ";" }

WHITESPACE = _{ " " | "\t" | NEWLINE }

polenta_inner =  { let_poly_expr | let_expr | expr }
polenta       = _{ SOI ~ polenta_inner+ ~ EOI }
